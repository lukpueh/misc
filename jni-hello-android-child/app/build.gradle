apply plugin: 'com.android.model.application'

// Root of Python lib and headers
def lib_distribution_root = '../distribution'

model {
    repositories {
        libs(PrebuiltLibraries) {
            python27 {
                // Python header files
                headers.srcDir "${lib_distribution_root}/python27/include"
                // Python shared library -- each ABI should have a lib file
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("${lib_distribution_root}/python27/lib/${targetPlatform.getName()}/libpython2.7.so")
                }
            }
        }
    }
    android {
        compileSdkVersion 24
        buildToolsVersion "24.0.0"

        defaultConfig {
            applicationId "com.google.sample.helloandroidjni"
            minSdkVersion.apiLevel 13
            targetSdkVersion.apiLevel 24
            versionCode 1
            versionName "1.0"
        }
        ndk {
            platformVersion 21
            moduleName "hello-android-jni"
            toolchain = 'clang'
            abiFilters.addAll(['armeabi-v7a'])
            ldLibs.addAll(["android", "log"])
        }
        sources {
            main {
                jni {
                    dependencies {
                        library 'python27' linkage 'shared'
                    }
                }
                jniLibs {
                    source {
                        srcDir "${lib_distribution_root}/python27/lib"
                    }
                }
            }
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles.add(file('proguard-android.txt'))
            }
            // debug build type is implicit
        }
    }
}

/*
* XXX LP: Do we need all those default dependencies?
* */
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
     androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
         exclude group: 'com.android.support', module: 'support-annotations'
     })
    compile 'com.android.support:appcompat-v7:24.0.0'
     compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha4'
     compile 'com.android.support:design:24.0.0'
     testCompile 'junit:junit:4.12'
}